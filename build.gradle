import org.apache.tools.ant.filters.ReplaceTokens

plugins {
	id "java"
	id "maven-publish"
	id "de.undercouch.download" version "+"
}

group "org.mtr.core"
version rootProject.properties.version

repositories {
	mavenCentral()
	maven { url "https://jitpack.io" }
}

dependencies {
	implementation "com.google.code.gson:gson:+"
	implementation "it.unimi.dsi:fastutil:+"
	implementation "com.corundumstudio.socketio:netty-socketio:+"
	implementation "org.msgpack:msgpack-core:+"
	implementation "com.github.jonafanho:Socket-IO-Webserver:master-SNAPSHOT"
}

java {
	toolchain {
		languageVersion.set(JavaLanguageVersion.of(8))
	}
	withSourcesJar()
	withJavadocJar()
}

publishing {
	publications {
		maven(MavenPublication) {
			from components.java
		}
	}
}

jar {
	manifest {
		attributes("Main-Class": "org.mtr.core.Main")
	}
}

tasks.register("downloadResources") {
	download.run {
		src "https://raw.githubusercontent.com/mrdoob/three.js/dev/build/three.module.min.js"
		dest "src/main/resources/org/mtr/core/website/three.module.min.js"
		overwrite true
		retries - 1
	}
	download.run {
		src "https://raw.githubusercontent.com/mrdoob/three.js/dev/examples/jsm/utils/BufferGeometryUtils.js"
		dest "src/main/resources/org/mtr/core/website/temp/BufferGeometryUtils.js"
		overwrite true
		retries - 1
	}
	copy {
		outputs.upToDateWhen { false }
		from "src/main/resources/org/mtr/core/website/temp/BufferGeometryUtils.js"
		into "src/main/resources/org/mtr/core/website"
		filter { line -> line.replaceAll("from 'three'", "from './three.module.min.js'") }
	}
	copy {
		outputs.upToDateWhen { false }
		from "src/main/templates/version-template.js"
		into "src/main/resources/org/mtr/core/website"
		filter(ReplaceTokens, tokens: ["version": version])
		rename "(.+)-template.js", "\$1.js"
	}
}

tasks.build.dependsOn downloadResources
